apply plugin: 'java'

apply from: "$gradleScriptsDir/javadoc.gradle"

apply from: "$gradleScriptsDir/google-java-format.gradle"
apply from: "$gradleScriptsDir/checkstyle.gradle"
apply from: "$gradleScriptsDir/pmd.gradle"
apply from: "$gradleScriptsDir/spotbugs.gradle"

apply from: "$gradleScriptsDir/test.gradle"
apply from: "$gradleScriptsDir/integration-test.gradle"
apply from: "$gradleScriptsDir/production-test.gradle"
apply from: "$gradleScriptsDir/jacoco.gradle"

apply from: "$gradleScriptsDir/license.gradle"
apply from: "$gradleScriptsDir/owasp.gradle"

dependencies {
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-library:1.3'
}

sourceCompatibility = '12'
targetCompatibility = '12'

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs = ['-Xlint:none']
  if(rootProject.generateDocumentationEnabled == 'true') {
    options.compilerArgs += '-Xdoclint:none'
  }
}

task sourcesJar(type: Jar) {
  group = 'build'
  description = 'Assembles a jar archive containing the main sources.'
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  if(rootProject.packageSourcesEnabled == 'true') {
    archives sourcesJar
  }
}

jar {
  manifest {
    attributes 'Implementation-Title': project.name,
               'Implementation-Version': project.version,
               'Built-By': System.getProperty('user.name'),
               'Built-Date': new Date(),
               'Built-JDK': System.getProperty('java.version'),
               'Built-Gradle': gradle.gradleVersion
  }
}